//It all starts here
buildscript {
  repositories {
    mavenLocal()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://repo.grails.org/grails/core" }
  }
  dependencies {
    classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    classpath "org.yakworks:gradle-plugins:1.0.9"
  }
}

apply plugin: 'yakworks.shipyak' //our opinionated defaults for gradle groovy & grails projects

subprojects {
  ['org.grails.grails-plugin', 'org.grails.grails-web'].each { plugId ->
    plugins.withId(plugId) {
      sourceCompatibility = '1.8'
      targetCompatibility = '1.8'

      dependencies {
        testRuntime "com.h2database:h2"
        testRuntime "org.apache.tomcat:tomcat-jdbc"

        testCompile "org.grails:grails-gorm-testing-support"
        testCompile "org.grails:grails-web-testing-support"
        testCompile "org.grails.plugins:build-test-data:$versionBuildTestData"
        testCompile "org.grails:views-json-testing-support:$versionViews"
      }
      test {
        //This way Spock/Groovy power asserts will be  visiblein the build log
        //Very useful - you don't have to look at the test results to debug a test failure
        testLogging.exceptionFormat = 'full'
        //The below seems to help  with "Process 'Gradle Worker Daemon 1' finished with non-zero exit value 137" in circle
        //https://stackoverflow.com/questions/38967991/why-are-my-gradle-builds-dying-with-exit-code-137
        //maxHeapSize '512m'
        jvmArgs "-XX:+UnlockExperimentalVMOptions", "-XX:+UseCGroupMemoryLimitForHeap"
      }
    }
  }
}
